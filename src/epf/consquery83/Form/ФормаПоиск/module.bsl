////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мМожноПродолжитьПоиск;

Перем мНастройкиПрокси;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция УстановитьВидимостьДоступность()
	ЭлементыФормы.ПанельРезультатыПоиска.Страницы.Облако.Видимость = ИскатьВОблаке;
	ЭлементыФормы.ПанельРезультатыПоиска.Страницы.Локально.Видимость = ИскатьЛокально;
КонецФункции // УстановитьВидимостьДоступность()

Процедура ИзменитьМестоПоискаДействие(Элемент)
	
	// запросы / алгоритмы
	Если Не ИскатьВЗапросах И Не ИскатьВАлгоритмах Тогда 
		Если Элемент = Неопределено Тогда 
			ИскатьВЗапросах = Истина;
			ИскатьВАлгоритмах = Истина;
		ИначеЕсли Элемент.Имя = "ИскатьВАлгоритмах" Тогда 
			ИскатьВЗапросах = Истина;
		Иначе 
			ИскатьВАлгоритмах = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// название / текст
	Если Не ИскатьВНазваниях И Не ИскатьВТексте Тогда 
		Если Элемент = Неопределено Тогда 
			ИскатьВНазваниях = Истина;
			ИскатьВТексте = Истина;
		ИначеЕсли Элемент.Имя = "ИскатьВНазваниях" Тогда 
			ИскатьВТексте = Истина;
		Иначе 
			ИскатьВНазваниях = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// локально / в облаке
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	Если Элемент <> Неопределено И Элемент.Имя = "ИскатьВОблаке" И ИскатьВОблаке И Не ЗначениеЗаполнено(лСоединение) Тогда 
		ПоказатьПредупреждение(, "Для поиска в облаке необходимо к нему подключиться.");
		ИскатьВОблаке = Ложь;
		ИскатьЛокально = Истина;
	Иначе
		Если Не ИскатьВОблаке И Не ИскатьЛокально Тогда 
			Если Элемент = Неопределено Тогда 
				ИскатьЛокально = Истина;
				Если ЗначениеЗаполнено(лСоединение) Тогда 
					ИскатьВОблаке = Истина;
				КонецЕсли;
			ИначеЕсли Элемент.Имя = "ИскатьВОблаке" Тогда 
				ИскатьЛокально = Истина;
			ИначеЕсли ЗначениеЗаполнено(лСоединение) Тогда 
				ИскатьВОблаке = Истина;
			Иначе
				ПоказатьПредупреждение(, "Должна быть указана хотя бы одна область поиска.");
				ИскатьЛокально = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры // изменитьМестоПоискаДействие()

// рекурсивная процедура поиска текста
Функция НайтиЗапросыПоСтруктуреПоискаРекурсивно(СтруктураДляПоиска, Дерево)
	
	Для каждого ТекСтрока Из Дерево.Строки Цикл
		Если СтруктураДляПоиска.ИскатьВТексте тогда
			Если СтруктураДляПоиска.ИскатьВТексте И Найти(НРег(ТекСтрока.ТекстЗапроса), НРег(СтруктураДляПоиска.СтрокаПоиска)) > 0 
				ИЛИ СтруктураДляПоиска.ИскатьВНазваниях И Найти(НРег(ТекСтрока.Имя), НРег(СтруктураДляПоиска.СтрокаПоиска)) > 0 тогда
				НоваяСтрока               = РезультатПоиска.Добавить();
				НоваяСтрока.Тип           = 0;
				НоваяСтрока.СтрокаЗапроса = ТекСтрока.Идентификатор;
				НоваяСтрока.ИмяЗапроса    = ТекСтрока.Имя;
			КонецЕсли;
		КонецЕсли;
		НайтиЗапросыПоСтруктуреПоискаРекурсивно(СтруктураДляПоиска, ТекСтрока)
	КонецЦикла; 
	
	Возврат Истина;
	
КонецФункции // НайтиЗапросыПоСтруктуреПоискаРекурсивно()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	ИзменитьМестоПоискаДействие(Неопределено);
	УстановитьВидимостьДоступность();
	ТекущийЭлемент = ЭлементыФормы.СтрокаПоиска;
	
	мНастройкиПрокси = гВосстановитьНастройкиПрокси();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КоманднаяПанельВОблакеПродолжитьПоиск(Кнопка)
	ОбработкаДействияНайти(Истина)
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура НайтиНажатие(Элемент)
	ОбработкаДействияНайти();
КонецПроцедуры

Процедура ОбработкаДействияНайти(Продолжение = Ложь)
	
	Если ИскатьЛокально Тогда 		
		РезультатПоиска.Очистить();
		
		Если ИскатьВЗапросах Тогда 
			
			лСтруктураПоиска = Новый Структура();
			лСтруктураПоиска.Вставить("ИскатьВНазваниях", ИскатьВНазваниях);
			лСтруктураПоиска.Вставить("ИскатьВТексте"   , ИскатьВТексте);
			лСтруктураПоиска.Вставить("ИскатьВОблаке"   , ИскатьВОблаке);
			лСтруктураПоиска.Вставить("ИскатьЛокально"  , ИскатьЛокально);
			лСтруктураПоиска.Вставить("СтрокаПоиска"    , СтрокаПоиска);			
			
			НайтиЗапросыПоСтруктуреПоискаРекурсивно(лСтруктураПоиска, ВладелецФормы.ДеревоЗапросов);		
		КонецЕсли;
			
		Если ИскатьВАлгоритмах Тогда
			Для каждого СтрокаСКодом Из ВладелецФормы.ИсполняемыйКод Цикл
				Если ИскатьВТексте И Найти(НРег(СтрокаСКодом.Текст), НРег(СтрокаПоиска)) > 0 ИЛИ 
					ИскатьВНазваниях И Найти(НРег(СтрокаСКодом.Имя), НРег(СтрокаПоиска)) тогда
					НоваяСтрока               = РезультатПоиска.Добавить();
					НоваяСтрока.Тип           = 1;
					НоваяСтрока.СтрокаКода    = СтрокаСКодом.ИдентификаторСтроки;
					НоваяСтрока.ИмяКода       = СтрокаСКодом.Имя;
					
					лНайденнаяСтрокаЗапроса   = ВладелецФормы.ДеревоЗапросов.Строки.Найти(СтрокаСКодом.ИдентификаторЗапроса, "Идентификатор", Истина);
					НоваяСтрока.СтрокаЗапроса = лНайденнаяСтрокаЗапроса.Идентификатор;
					НоваяСтрока.ИмяЗапроса    = лНайденнаяСтрокаЗапроса.Имя;
				КонецЕсли;
			КонецЦикла; 
		КонецЕсли;
	КонецЕсли;
		
	Если ИскатьВОблаке Тогда 
		
		Если Продолжение И мМожноПродолжитьПоиск Тогда 
			лНачальнаяПозицияПоиска = РезультатПоискаВОблаке.Количество();
		Иначе
			лНачальнаяПозицияПоиска = 0;
			РезультатПоискаВОблаке.Очистить();
		КонецЕсли;
		
		ЭлементыФормы.КоманднаяПанельВОблаке.Кнопки.ПродолжитьПоиск.Доступность = Ложь;
		
		лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
		лРезультатПоискаВОблаке = гПоискВОблаке(лСоединение, СтрокаПоиска, ИскатьВЗапросах, ИскатьВАлгоритмах, ИскатьВНазваниях, ИскатьВТексте, лНачальнаяПозицияПоиска, мНастройкиПрокси);
		
		Если лРезультатПоискаВОблаке.Статус = "OK" Тогда 
			мМожноПродолжитьПоиск = лРезультатПоискаВОблаке.МожноПродолжитьПоиск;
			ЭлементыФормы.КоманднаяПанельВОблаке.Кнопки.ПродолжитьПоиск.Доступность = мМожноПродолжитьПоиск;
			Для каждого СтрокаСЗапросом Из лРезультатПоискаВОблаке.ДанныеИзОблака Цикл
				НоваяСтрока = РезультатПоискаВОблаке.Добавить();
				НоваяСтрока.Тип           = СтрокаСЗапросом.type;
				НоваяСтрока.СтрокаЗапроса = СтрокаСЗапросом.idFromApp;
				НоваяСтрока.ИмяЗапроса    = СтрокаСЗапросом.nameQuery;
				Если ЗначениеЗаполнено(СтрокаСЗапросом.nameCode) Тогда 
					НоваяСтрока.ИмяКода    = СтрокаСЗапросом.nameCode;
					НоваяСтрока.СтрокаКода = СтрокаСЗапросом.idRowFromApp;
				КонецЕсли;
				
				НоваяСтрока.имяФайла = СтрокаСЗапросом.fileName;
				НоваяСтрока.идФайла  = СтрокаСЗапросом.idPackage;				
			КонецЦикла; 
		Иначе
			Сообщить("Ошибка поиска в облаке: " + лРезультатПоискаВОблаке.ТекстОшибки);
		КонецЕсли;
	Иначе
		лРезультатПоискаВОблаке = Неопределено
	КонецЕсли;	
		
	Если Не Продолжение Тогда 
		Если РезультатПоиска.Количество() > 0 тогда
			ЭлементыФормы.ПанельРезультатыПоиска.ТекущаяСтраница = ЭлементыФормы.ПанельРезультатыПоиска.Страницы.Локально;
		ИначеЕсли РезультатПоискаВОблаке.Количество() > 0 тогда
			ЭлементыФормы.ПанельРезультатыПоиска.ТекущаяСтраница = ЭлементыФормы.ПанельРезультатыПоиска.Страницы.Облако;
		Иначе
			ПоказатьПредупреждение(, "Совпадений не найдено.", 2);
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаДействияНайти()

Процедура ИскатьВОблакеПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

Процедура ИскатьЛокальноПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

Процедура ИскатьВЗапросахПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

Процедура ИскатьВАлгоритмахПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

Процедура ИскатьВНазванияхПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

Процедура ИскатьВТекстеПриИзменении(Элемент)
	ИзменитьМестоПоискаДействие(Элемент);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ РезультатПоиска

Процедура РезультатПоискаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если ВыбраннаяСтрока.Тип = 0 ИЛИ ВыбраннаяСтрока.Тип = 2 тогда
		// позиционируем дерево с запросами на данной строке
		ВладелецФормы.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ВладелецФормы.ДеревоЗапросов.Строки.Найти(ВыбраннаяСтрока.СтрокаЗапроса, "Идентификатор", Истина);
		ВладелецФормы.Активизировать();
	ИначеЕсли ВыбраннаяСтрока.Тип = 1 тогда
		
		// позиционируем дерево с запросами на данной строке и открываем код
		ФормаВариантовКода = ВладелецФормы.мФормаИсполняемыйКод;
		Если ФормаВариантовКода.Открыта() Тогда 
			ФормаВариантовКода.Закрыть();
		КонецЕсли;
		ВладелецФормы.ЭлементыФормы.ДеревоЗапросов.ТекущаяСтрока = ВладелецФормы.ДеревоЗапросов.Строки.Найти(ВыбраннаяСтрока.СтрокаЗапроса, "Идентификатор", Истина);
		ФормаВариантовКода.Открыть();
		ФормаВариантовКода.ЭлементыФормы.ИсполняемыйКодСписок.ТекущаяСтрока = ФормаВариантовКода.ИсполняемыйКодСписок.Найти(ВыбраннаяСтрока.СтрокаКода, "ИдентификаторСтроки");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ РезультатПоискаВОблаке

Процедура РезультатПоискаВОблакеВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
	лРезультат = гПолучитьДанныеДляПревьюИзОблака(лСоединение, ВыбраннаяСтрока.идФайла, ВыбраннаяСтрока.СтрокаЗапроса, ВыбраннаяСтрока.СтрокаКода);
	
	Если лРезультат.Статус = "OK" Тогда 
		лФорма = ПолучитьФорму("ВнешняяОбработка.КонсольЗапросов.Форма.ФормаПредпросмотрИзОблака",, ЭтаФорма);
		лФорма.Заголовок = "Предпросмотр (запрос " + ?(ЗначениеЗаполнено(ВыбраннаяСтрока.ИмяКода), " + алгоритм ", "") + "): " + ВыбраннаяСтрока.имяФайла + " / " + ВыбраннаяСтрока.ИмяЗапроса + ?(ЗначениеЗаполнено(ВыбраннаяСтрока.ИмяКода), " / " + ВыбраннаяСтрока.ИмяКода, "" );
		лФорма.идПакета = лРезультат.ДанныеИзОблака.idPackage;
		лФорма.идЗапроса = лРезультат.ДанныеИзОблака.idQuery;
		лФорма.идКода = лРезультат.ДанныеИзОблака.idCode;
		лФорма.полноеИмяФайла = ВыбраннаяСтрока.имяФайла;		
		лФорма.Элементыформы.ТекстЗапроса.УстановитьТекст(лРезультат.ДанныеИзОблака.textQuery);
		лФорма.Элементыформы.КодДляВыполнения.УстановитьТекст(лРезультат.ДанныеИзОблака.textCode);
		лФорма.Открыть();
	Иначе
		Сообщить("Ошибка поиска в облаке: " + лРезультат.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры
