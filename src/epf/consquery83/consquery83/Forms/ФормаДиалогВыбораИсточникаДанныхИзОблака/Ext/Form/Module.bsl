////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мТекущийПакет, мТекущийПуть, мПредыдущийЭлемент, мИндексыКартинкиДляТипов;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

Функция ПолучитьИдЗапросаПоПути(Путь)
	Если Не ЗначениеЗаполнено(Путь) ИЛИ Путь.Количество() = 0 Тогда 
		Возврат Неопределено
	Иначе
		Возврат Путь[0].id
	КонецЕсли;
КонецФункции // ПолучитьИдЗапросаПоПути()

Функция типыИсточниковДанных()
	Возврат Новый Структура("Пакет, Запрос, Код", "Пакет", "Запрос", "Код");
КонецФункции

Процедура КнопкаВыполнитьНажатиеДействие()
	
	Если Не ЗначениеЗаполнено(ИмяЭлементаИзОблака) Тогда 
		ПоказатьПредупреждение(, "Введите имя элемента.");
		Возврат;		
	КонецЕсли;
	
	Если гЭтоЗапись(Режим) и Тип <> типЭлемента Тогда 
		ПоказатьПредупреждение(, "Выберите элемент с типом " + Тип +" или введите новый.");
		Возврат;
	КонецЕсли;
	
	Если гЭтоЗапись(Режим) и Тип = типыИсточниковДанных().Код И ПолучитьИдЗапросаПоПути(мТекущийПуть) = Неопределено Тогда 
		ПоказатьПредупреждение(, "Выберите запрос, для которого необходимо сохранить код.");
		Возврат;
	КонецЕсли;
	
	лСтруктураПараметров = Новый Структура;
	лСтруктураПараметров.Вставить("Выбран"                    , Истина);
	лСтруктураПараметров.Вставить("Имя"                       , ИмяЭлементаИзОблака);
	лСтруктураПараметров.Вставить("Тип"                       , типЭлемента);
	лСтруктураПараметров.Вставить("ИдПакета"                  , мТекущийПакет);
	лСтруктураПараметров.Вставить("ИдРодительскогоЗапроса"    , ПолучитьИдЗапросаПоПути(мТекущийПуть));
	лСтруктураПараметров.Вставить("ИдЗапроса"                 , ПолучитьИдЗапросаПоПути(мТекущийПуть));
	лСтруктураПараметров.Вставить("ИдКода"                    , Неопределено);
	
	// далее дозаполняются! ключи: ИдПакета, ИдРодительскогоЗапроса, ИдЗапроса, ИДКода, ВключатьПодчиненныеЗапросы
	
	лВыбранныйЭлементМассив = СписокЭлементовИзОблака.НайтиСтроки(Новый Структура("Имя, Тип", ИмяЭлементаИзОблака, типЭлемента));
	
	лВыбранныйЭлемент = ?(лВыбранныйЭлементМассив.Количество() > 0, лВыбранныйЭлементМассив[0], Неопределено);
	
	Если типЭлемента = типыИсточниковДанных().Запрос Тогда 
		лСтруктураПараметров.Вставить("ВключатьПодчиненныеЗапросы", Не НеВключатьПодчиненныеЗапросы);
	КонецЕсли;
	
	Если лВыбранныйЭлемент <> Неопределено Тогда 
		лИдентификатор = лВыбранныйЭлемент.id;
		
		Если типЭлемента = типыИсточниковДанных().Код Тогда 
			лСтруктураПараметров.Вставить("ИдКода"   , лИдентификатор);
		ИначеЕсли типЭлемента = типыИсточниковДанных().Запрос Тогда 
			лСтруктураПараметров.Вставить("ИдЗапроса", лИдентификатор);
		ИначеЕсли типЭлемента = типыИсточниковДанных().Пакет Тогда 
			лСтруктураПараметров.Вставить("ИдПакета", лИдентификатор);
		КонецЕсли;
		
		Если Режим = гОперацииСЗапросами().СохранитьКАК Тогда 
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаРезультатаВопроса", ЭтаФорма, лСтруктураПараметров),
				"""" + ИмяЭлементаИзОблака + """ уже существует. 
					|Вы хотите его перезаписать",
				РежимДиалогаВопрос.ДаНет,,
				КодВозвратаДиалога.Да,
				"Сохранить как...");
			Возврат;
		КонецЕсли;
	ИначеЕсли гЭтоЗапись(Режим) Тогда 
		Если типЭлемента = типыИсточниковДанных().Код Тогда 
			лСтруктураПараметров.Вставить("ИдКода"   , гИдентификаторНовогоОбъектаВОблаке());
		ИначеЕсли типЭлемента = типыИсточниковДанных().Запрос Тогда 
			лСтруктураПараметров.Вставить("ИдЗапроса", гИдентификаторНовогоОбъектаВОблаке());
		ИначеЕсли типЭлемента = типыИсточниковДанных().Пакет Тогда 
			лСтруктураПараметров.Вставить("ИдПакета", гИдентификаторНовогоОбъектаВОблаке());
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, "Выберите существующий пакет с запросами.");
		Возврат;
	КонецЕсли;
	
	Закрыть(лСтруктураПараметров);

КонецПроцедуры // КнопкаВыполнитьНажатиеДействие()
	
Процедура ОбновитьСтрокуСПутемКЗапросу(ИмяПакета)
	
	Если гЭтоЗапись(Режим) Тогда 
		Если Тип = гТипыИсточниковДанных().Пакет Тогда 
			Заголовок = "Выберите пакет для записи";
		ИначеЕсли Тип = гТипыИсточниковДанных().Запрос Тогда 
			Заголовок = "Выберите запрос для записи";
		ИначеЕсли Тип = гТипыИсточниковДанных().Код Тогда 
			Заголовок = "Выберите код для записи";
		Иначе
			Заголовок = "ошибка: неопределен тип для записи";
		КонецЕсли;
	Иначе
		Если ИмяПакета = Неопределено Тогда 
			Заголовок = "Выберите пакет с запросами";
		Иначе
			Заголовок = "Выберите запрос или код";
		КонецЕсли;	
	КонецЕсли;
	
	Если ИмяПакета = Неопределено Тогда 
		Путь = "";
	Иначе
		лПутьВнутриПакета = "";
		Если ЗначениеЗаполнено(мТекущийПуть) Тогда 
			Для Каждого Строка Из мТекущийПуть Цикл
				лПутьВнутриПакета = Строка.name + " \ " + лПутьВнутриПакета;
			КонецЦикла;
		КонецЕсли;
		Путь = "[" + ИмяПакета + "] \ " + лПутьВнутриПакета;
	КонецЕсли;	
КонецПроцедуры // ОбновитьСтрокуСПутемКЗапросу()

Процедура УстановитьВидимостьНеВключатьПодчиненныеЗапросы()
	ЭлементыФормы.НеВключатьПодчиненныеЗапросы.Видимость = (ТипЭлемента = ТипыИсточниковДанных().Запрос);
КонецПроцедуры	

Процедура ЗаполнитьСписокДаннымиИзОблака(лДанныеОблака, ЭтоСписокПакетов)
	
	СписокЭлементовИзОблака.Очистить();
	Если ЭтоСписокПакетов Тогда 
		Для каждого лДанныеПакета Из лДанныеОблака.СписокПакетов Цикл
			лНоваяСтрока = СписокЭлементовИзОблака.Добавить();
			лНоваяСтрока.Имя         = лДанныеПакета.name;
			лНоваяСтрока.Тип         = типыИсточниковДанных().Пакет;
			лНоваяСтрока.ТипКартинка = мИндексыКартинкиДляТипов.Получить(лНоваяСтрока.Тип);
			лНоваяСтрока.Создан      = лДанныеПакета.dateCreate;
			лНоваяСтрока.Изменен     = лДанныеПакета.dateMod;
			лНоваяСтрока.Информация  = лДанныеПакета.idUser + " | Запросов: " + лДанныеПакета.queryCount;
			лНоваяСтрока.id          = лДанныеПакета.id;
			
		КонецЦикла; 
		мТекущийПуть   = Неопределено;
		мТекущийПакет  = "-1";
		
		ОбновитьСтрокуСПутемКЗапросу(Неопределено);
		
		ДобавитьПунктКонтекстногоМенюСоздатьНовыйПакет();
		
	Иначе
		лТипЭлементаСписка = Неопределено;
		
		Для каждого лДанныеПакета Из лДанныеОблака.Список Цикл
			лНоваяСтрока = СписокЭлементовИзОблака.Добавить();
			лНоваяСтрока.Имя        = лДанныеПакета.name;
			лНоваяСтрока.Создан     = лДанныеПакета.dateCreate;
			лНоваяСтрока.Изменен    = лДанныеПакета.dateMod;
			лНоваяСтрока.Информация = лДанныеПакета.info;
			лНоваяСтрока.id         = лДанныеПакета.id;
			
			Если типыИсточниковДанных().Свойство(лДанныеПакета.type, лТипЭлементаСписка) = Неопределено Тогда 
				лНоваяСтрока.Тип = "не определен";
			Иначе
				лНоваяСтрока.Тип = лТипЭлементаСписка;
			КонецЕсли;			
			
			лНоваяСтрока.ТипКартинка = мИндексыКартинкиДляТипов.Получить(лНоваяСтрока.Тип);
			
		КонецЦикла;
		
		мТекущийПуть  = лДанныеОблака.ПутьКРодителю;
		мТекущийПакет = лДанныеОблака.идПакета;
		
		ОбновитьСтрокуСПутемКЗапросу(лДанныеОблака.ИмяПакета);
		
		УдалитьПунктКонтекстногоМенюСоздатьНовыйПакет();		
		
	КонецЕсли;
КонецПроцедуры // ЗаполнитьСписокДаннымиИзОблака()

&НаКлиенте
Процедура ЗаполнитьЗапросамиИлиПакетамиИзОблакаПоИсторииИспользования(Отказ)
	
	лИдентификаторСессии = гВосстановитьИдентификаторСессииConsqueryCloud();
	лДанныеОблака        = гПолучитьСписокПакетовИЛИЗапросовВОблакеПоИсторииИспользования(лИдентификаторСессии);

	Если лДанныеОблака.Статус <> "OK" Тогда 
		Сообщить(лДанныеОблака.ТекстОшибки, СтатусСообщения.Важное);		
		Если лДанныеОблака.НеобходимоПереподключиться Тогда 
			Оповестить("НеобходимоПереподключиться", Истина, ЭтаФорма);
		КонецЕсли;
		Отказ = Истина;
	Иначе
		ЗаполнитьСписокДаннымиИзОблака(лДанныеОблака, лДанныеОблака.ЭтоСписокПакетов);
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЗапросамиИлиПакетамиИзОблакаПоИсторииИспользования()

Процедура ЗаполнитьПакетамиИзОблака(Отказ)
	
	лСоединение   = гВосстановитьИдентификаторСессииConsqueryCloud();
	лДанныеОблака = гПолучитьСписокПакетовВОблаке(лСоединение);
	
	Если лДанныеОблака.Статус <> "OK" Тогда 
		Сообщить(лДанныеОблака.ТекстОшибки, СтатусСообщения.Важное);		
		Если лДанныеОблака.НеобходимоПереподключиться Тогда 
			Оповестить("НеобходимоПереподключиться", Истина, ЭтаФорма);
		КонецЕсли;
		Отказ = Истина;
	Иначе
		ЗаполнитьСписокДаннымиИзОблака(лДанныеОблака, Истина)
	КонецЕсли;
КонецПроцедуры // ЗаполнитьПакетамиИзОблака()

Процедура ЗаполнитьЗапросамиИзОблака(идПакета, идРодителя, Отказ)
	
	лСоединение   = гВосстановитьИдентификаторСессииConsqueryCloud();
	лДанныеОблака = гПолучитьСписокЗапросовВОблаке(лСоединение, идПакета, идРодителя);
	
	Если лДанныеОблака.Статус <> "OK" Тогда 
		Сообщить(лДанныеОблака.ТекстОшибки, СтатусСообщения.Важное);		
		Если лДанныеОблака.НеобходимоПереподключиться Тогда 
			Оповестить("НеобходимоПереподключиться", Истина, ЭтаФорма);
		КонецЕсли;
		Отказ = Истина;
	Иначе
		ЗаполнитьСписокДаннымиИзОблака(лДанныеОблака, Ложь)
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьЗапросамиИзОблака()

Процедура ОбновитьДействие(Отказ = Ложь)
	
	Если мТекущийПакет = Неопределено ИЛИ мТекущийПакет = "-1" Тогда 
		ЗаполнитьПакетамиИзОблака(Отказ);
	Иначе
		идРодителя = ?(ЗначениеЗаполнено(мТекущийПуть), мТекущийПуть[0].id, Неопределено);
		ЗаполнитьЗапросамиИзОблака(мТекущийПакет, идРодителя, Отказ);
	КонецЕсли;
	
КонецПроцедуры // ОбновитьДействие()

Процедура ЗакрытьФормуСВыбором(ПараметрыЗакрытия)	
	Закрыть(ПараметрыЗакрытия);	
КонецПроцедуры // закрытьформуСВыбором()

Функция ПолучитьJSONЗначения(Значение)
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(,,,,,,Истина));
	ЗаписатьJSON(ЗаписьJSON, Значение, Новый НастройкиСериализацииJSON);
	Возврат ЗаписьJSON.Закрыть();
КонецФункции

Процедура ДобавитьПунктКонтекстногоМенюСоздатьНовыйПакет()
	
	КнопкаПодменю = ЭлементыФормы.КоманднаяПанельКонтМеню.Кнопки.МенюСпискаЭлементовВОблаке;
	КнопкаПодменю.Кнопки.Очистить();
	
	ДобавленнаяКнопка = КнопкаПодменю.Кнопки.Добавить(
		"Обновить",
		ТипКнопкиКоманднойПанели.Действие,
		"Обновить",
		Новый Действие("КоманднаяПанельКонтМенюОбновить"));
		
	ДобавленнаяКнопка.Картинка = БиблиотекаКартинок.Обновить;
		
	ДобавленнаяКнопка = КнопкаПодменю.Кнопки.Добавить(                                                                  
		"СоздатьНовыйПакет",
		ТипКнопкиКоманднойПанели.Действие,
		"Создать новый пакет",
		Новый Действие("КоманднаяПанельКонтМенюСоздатьНовыйПакет"));
		
	ДобавленнаяКнопка.Картинка = БиблиотекаКартинок.СоздатьГруппу;
	
	ДобавленнаяКнопка = КнопкаПодменю.Кнопки.Добавить(                                                                  
		"Удалить",
		ТипКнопкиКоманднойПанели.Действие,
		"Удалить",
		Новый Действие("КоманднаяПанельКонтМенюУдалить"));
		
	ДобавленнаяКнопка.Картинка = БиблиотекаКартинок.Удалить;
	
КонецПроцедуры // ДобавитьПунктКонтекстногоМенюСоздатьНовыйПакет()

Процедура УдалитьПунктКонтекстногоМенюСоздатьНовыйПакет()
	
	КнопкаПодменю = ЭлементыФормы.КоманднаяПанельКонтМеню.Кнопки.МенюСпискаЭлементовВОблаке;
	КнопкаПодменю.Кнопки.Очистить();
	
	ДобавленнаяКнопка = КнопкаПодменю.Кнопки.Добавить(
		"Обновить",
		ТипКнопкиКоманднойПанели.Действие,
		"Обновить",
		Новый Действие("КоманднаяПанельКонтМенюОбновить"));
		
	ДобавленнаяКнопка.Картинка = БиблиотекаКартинок.Обновить;
		
	ДобавленнаяКнопка = КнопкаПодменю.Кнопки.Добавить(                                                                  
		"Удалить",
		ТипКнопкиКоманднойПанели.Действие,
		"Удалить",
		Новый Действие("КоманднаяПанельКонтМенюУдалить"));
		
	ДобавленнаяКнопка.Картинка = БиблиотекаКартинок.Удалить;
		
КонецПроцедуры // УдалитьПунктКонтекстногоМенюСоздатьНовыйПакет()

// ЗавершенияАсинхронныхВызовов

Процедура ПослеЗакрытияВопросаУдалитьПакет(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		лСоединение       = гВосстановитьИдентификаторСессииConsqueryCloud();
		лСписокИдКодов    = Новый Массив;
		лСписокИдПакетов  = Новый Массив;
		лСписокИдЗапросов = Новый Массив;
		
		Для каждого лСтрокаСФайлом Из ЭлементыФормы.СписокЭлементовИзОблака.ВыделенныеСтроки Цикл
			Если лСтрокаСФайлом.Тип = типыИсточниковДанных().Пакет Тогда 
				лСписокИдПакетов.Добавить(лСтрокаСФайлом.id);
			ИначеЕсли лСтрокаСФайлом.Тип = типыИсточниковДанных().Запрос Тогда 
				лСписокИдЗапросов.Добавить(лСтрокаСФайлом.id);
			ИначеЕсли лСтрокаСФайлом.Тип = типыИсточниковДанных().Код Тогда 
				лСписокИдКодов.Добавить(лСтрокаСФайлом.id);
			КонецЕсли;
		КонецЦикла; 
		
		Если лСписокИдПакетов.Количество() > 0 Тогда 
			Результат = гУдалитьФайлыСЗапросамиВОблаке(лСоединение, ПолучитьJSONЗначения(лСписокИдПакетов));	
		Иначе
			Результат = гУдалитьЭлементыПоСпискуВОблаке(лСоединение, мТекущийПакет, ПолучитьJSONЗначения(лСписокИдЗапросов), ПолучитьJSONЗначения(лСписокИдКодов));	
		КонецЕсли;
		
		Если Результат.Статус <> "OK" Тогда 
			Сообщить(Результат.ТекстОшибки, СтатусСообщения.Важное);
		Иначе
			ОбновитьДействие();
			Если Не ПустаяСтрока(Результат.Информация) Тогда 
				Сообщить(Результат.Информация, СтатусСообщения.Информация);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПослеЗакрытияВопросаУдалитьПакет()

Процедура ПоказатьВводСтрокиСоздатьНовыйПакетПродолжить(Строка, ДополнительныеПараметры) Экспорт
	
	Если СписокЭлементовИзОблака.НайтиСтроки(Новый Структура("Имя", Строка)).Количество() = 0 Тогда 
		
		лСоединение = гВосстановитьИдентификаторСессииConsqueryCloud();
		
		Результат = гДобавитьНовыйПакетЗапросовВОблако(лСоединение, Строка);	

		Если Результат.Статус <> "OK" Тогда 
			Сообщить(Результат.ТекстОшибки, СтатусСообщения.Важное);
		Иначе
			ЗаполнитьПакетамиИзОблака(Неопределено);
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, "Пакет с именем [" + Строка + "] уже существует. Укажите другое имя пакета.");
		Возврат;
	КонецЕсли;
КонецПроцедуры // ПоказатьВводСтрокиСоздатьНовыйПакетПродолжить()

Процедура ОбработкаРезультатаВопроса(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ЗакрытьФормуСВыбором(ДополнительныеПараметры);
	КонецЕсли;
КонецПроцедуры	
	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриЗакрытииВладельца = Истина;
	мПредыдущийЭлемент            = Неопределено;
	
	мИндексыКартинкиДляТипов = гИндексыКартинокДляТиповИсточниковДанных();
	
	Если Тип = ТипыИсточниковДанных().Пакет Тогда 
		ЗаполнитьПакетамиИзОблака(Отказ);			
	Иначе
		ЗаполнитьЗапросамиИлиПакетамиИзОблакаПоИсторииИспользования(Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Если Режим = гОперацииСЗапросами().СохранитьКАК ИЛИ Режим = гОперацииСЗапросами().Сохранить ИЛИ Режим = гОперацииСЗапросами().СохранитьВОблако Тогда 
			ЭлементыФормы.КнопкаВыполнить.Заголовок = "Сохранить";
			типЭлемента = Тип;
		ИначеЕсли Режим = гОперацииСЗапросами().ЗагрузитьИзОблака Тогда 
			Если СписокЭлементовИзОблака.Количество() = 0 Тогда 
				ПоказатьПредупреждение(,"Вы еще не сохраняли запросы в облаке.", 10, "Диалог выбора файла в облаке");
				Отказ = Истина;
			Иначе
				ЭлементыФормы.КнопкаВыполнить.Заголовок = "Загрузить";
			КонецЕсли;
		Иначе
			Сообщить("Не определен режим открытия диалога. Обратитесь к разработчику.", СтатусСообщения.Важное); // #рефакторинг сделать более информативное сообщение, упростить отправку ошибки
			Отказ = Истина;
		КонецЕсли;
		
		Если не Отказ Тогда 
			УстановитьВидимостьНеВключатьПодчиненныеЗапросы();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры // ПередОткрытием()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

Процедура КоманднаяПанельКонтМенюОбновить(Кнопка)
	ОбновитьДействие();
КонецПроцедуры

Процедура КнопкаДомойНажатие(Элемент)
	ЗаполнитьПакетамиИзОблака(Неопределено);
КонецПроцедуры

Процедура КнопкаУровеньВверхНажатие(Элемент)
	
	Отказ = Ложь;
	
	Если мТекущийПакет = "-1" Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(мТекущийПуть) ИЛИ мТекущийПуть.Количество() = 0 Тогда 
		
		идТекущегоЭлемента = мТекущийПакет;
		ЗаполнитьПакетамиИзОблака(Отказ);		
	Иначе
		
		идТекущегоЭлемента = мТекущийПуть[0].id;
		
		Если мТекущийПуть.Количество() = 1 Тогда 
			идРодителя = Неопределено;
		Иначе
			идРодителя = мТекущийПуть[1].id; // выбираем второй элемент массива
		КонецЕсли;
		
		ЗаполнитьЗапросамиИзОблака(мТекущийПакет, идРодителя, Отказ);		
	КонецЕсли;
	
	Если Не Отказ Тогда 
		ЭлементыФормы.СписокЭлементовИзОблака.ТекущаяСтрока = СписокЭлементовИзОблака.Найти(идТекущегоЭлемента, "id");
		УстановитьВидимостьНеВключатьПодчиненныеЗапросы();
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Элемент)
	КнопкаВыполнитьНажатиеДействие();
КонецПроцедуры // КнопкаВыполнитьНажатие()

Процедура КнопкаОтменитьНажатие(Элемент)
	Закрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ СВОЙСТВ И КАТЕГОРИЙ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОГО ПОЛЯ <СписокЭлементовИзОблака>

Процедура СписокЭлементовИзОблакаПриАктивизацииСтроки(Элемент)
	лТекущиеДанные = Элемент.ТекущиеДанные;
	Если лТекущиеДанные <> Неопределено Тогда 
		// если список источников не пуст
		Если Не гЭтоЗапись(Режим) Тогда 
			ИмяЭлементаИзОблака = лТекущиеДанные.Имя;
			типЭлемента = лТекущиеДанные.Тип;
			УстановитьВидимостьНеВключатьПодчиненныеЗапросы();
			мПредыдущийЭлемент = Новый Структура("Имя, Тип", лТекущиеДанные.Имя, лТекущиеДанные.Тип);
		ИначеЕсли лТекущиеДанные <> Неопределено И лТекущиеДанные.Тип = Тип Тогда 
			Если мПредыдущийЭлемент <> Неопределено Тогда 
				ИмяЭлементаИзОблака = лТекущиеДанные.Имя;
			КонецЕсли;
			мПредыдущийЭлемент = Новый Структура("Имя, Тип", лТекущиеДанные.Имя, лТекущиеДанные.Тип);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура СписокЭлементовИзОблакаВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Отказ = Ложь;
	
	Если Тип = гТипыИсточниковДанных().Пакет ИЛИ ВыбраннаяСтрока.Тип = гТипыИсточниковДанных().Код Тогда 
		Если ВыбраннаяСтрока.Тип = Тип Тогда 
			ИмяЭлементаИзОблака = ВыбраннаяСтрока.Имя;
		КонецЕсли;
		КнопкаВыполнитьНажатиеДействие();
	Иначе
		Если ВыбраннаяСтрока.Тип = типыИсточниковДанных().Пакет Тогда 
			ЗаполнитьЗапросамиИзОблака(ВыбраннаяСтрока.id, Неопределено, Отказ);
		ИначеЕсли ВыбраннаяСтрока.Тип = типыИсточниковДанных().Запрос Тогда 
			ЗаполнитьЗапросамиИзОблака(мТекущийПакет, ВыбраннаяСтрока.id, Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанельКонтМенюУдалить(Кнопка)
	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеЗакрытияВопросаУдалитьПакет", ЭтаФорма, Новый Структура),
		"Удалить выбранные элементы?",
		РежимДиалогаВопрос.ДаНет,,
		КодВозвратаДиалога.Да,
		"Удаление сохраненных данных в облаке.");
КонецПроцедуры

Процедура КоманднаяПанельКонтМенюСоздатьНовыйПакет(Кнопка)
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ПоказатьВводСтрокиСоздатьНовыйПакетПродолжить", ЭтаФорма, Новый Структура),
		гСтроковыеКонстанты("ИмяНовогоПакетаЗапроса"), "Введите название пакета", 100, Истина);
КонецПроцедуры

Процедура СписокЭлементовИзОблакаПриПолученииДанных(Элемент, ОформленияСтрок)

	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Ячейки.Тип.ОтображатьКартинку = Истина;
		ОформлениеСтроки.Ячейки.Тип.ОтображатьТекст    = Ложь;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мТекущийПуть = Новый Массив;