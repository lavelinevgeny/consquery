&НаСервере
Функция ОбъектОбработки()
	Возврат РеквизитФормыВЗначение("Объект");
КонецФункции

&НаСервере
Функция гРежимыОткрытияФормы()
	Возврат ОбъектОбработки().гРежимыОткрытияФормы();
КонецФункции

&НаСервере
Процедура СоздатьРеквизитCРезультатомЗапроса(АдресРезультата)
	// ПРОЦЕДУРА ДУБЛЬ(!!!)
	
	лОбработкаОбъект  = ОбъектОбработки();
	лРеквизитыТаблицы = лОбработкаОбъект.гПолучитьМассивРеквизитовДляДобавленияТаблицыНаФорму(УникальныйИдентификатор, АдресРезультата, АдресСпискаСпецПолей);
	
	// изменяем структуру реквизитов формы
	Попытка
		ИзменитьРеквизиты(, лРеквизитыТаблицы.МассивРеквизитовУдаления);
	Исключение
	КонецПопытки; 
	
	// раздельно добавляем и удаляем, потомучто при удалении отсутствующего реквизита происходит исключение
	Попытка
		ИзменитьРеквизиты(лРеквизитыТаблицы.МассивРеквизитов, );
	Исключение
		лОбработкаОбъект.гПоказатьОшибкуНаСервере(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	лРезультат = ПолучитьИзВременногоХранилища(АдресРезультата).Скопировать();
	ЗначениеВРеквизитФормы(лРезультат, лОбработкаОбъект.гСвойстваРеквизитаРезультатЗапроса().ИмяРеквизита);
	
	лОбработкаОбъект.гВывестиТаблицуНаФорму(Элементы, АдресРезультата, АдресСпискаСпецПолей, УсловноеОформление);
	
КонецПроцедуры // СоздатьРеквизитCРезультатомЗапроса()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимОткрытияФормы = гРежимыОткрытияФормы().Расшифровка Тогда 
		АдресТаблицыЗначений = ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(Параметры.АдресТаблицыЗначений)[Параметры.ИдентификаторСтроки][Параметры.ИмяСпецПоля], УникальныйИдентификатор);
		СоздатьРеквизитCРезультатомЗапроса(АдресТаблицыЗначений);
	ИначеЕсли Параметры.РежимОткрытияФормы = гРежимыОткрытияФормы().ПоказатьТаблицу Тогда 
		СоздатьРеквизитCРезультатомЗапроса(Параметры.АдресТаблицыЗначений);
	Иначе
		Сообщить("Не определен режим открытия формы.", СтатусСообщения.Важное);
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()
