
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");  
	
     // установим внутреннее строковое представление типа значения объекта обработки
    ТипОбработка = ЗначениеВСтрокуВнутр(ТипЗнч(ОбработкаОбъект));

	СеансовыеДанные = Новый Структура();	
	СеансовыеДанные.Вставить("ИдентификаторСеанса", Новый УникальныйИдентификатор);
	СеансовыеДанные.Вставить("КЭШ"                , Новый Структура);
	
	Элементы.Пароль.РежимПароля = Не ПоказатьПароль;	
	
	email               = Параметры.email;
	ИдентификаторСессии = Параметры.ИдентификаторСессии;
	
	Если Не ЗначениеЗаполнено(email) Тогда 
		лСохраненныеЗначения = гВосстановитьНастройкиПараметровПодключения(ТипОбработка, СеансовыеДанные);
		Если ТипЗнч(лСохраненныеЗначения) = Тип("Структура") Тогда 
			лСохраненныеЗначения.Свойство("email"              , email);
			лСохраненныеЗначения.Свойство("ИдентификаторСессии", ИдентификаторСессии);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказатьПарольПриИзменении(Элемент)
	Элементы.Пароль.РежимПароля = Не ПоказатьПароль;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПароль(Команда)
	
	Результат = гЗарегистрироватьВОблаке(ТипОбработка, СеансовыеДанные, ИдентификаторСессии, email, пароль, истина);
	
	Если Результат.Статус = "OK" Тогда 
		СтрокаСостояния = Результат.Информация;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		СтрокаСостояния = Результат.ТекстОшибки;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
КонецПроцедуры // ИзменитьПароль()

&НаКлиенте
Процедура ПроверитьПодключениеКОблаку(Команда)
	Результат = гПодключитьКОблаку(ТипОбработка, СеансовыеДанные, ИдентификаторСессии, email, пароль, Истина);
	
	Если Результат.Статус = "OK" Тогда 
		СтрокаСостояния = Результат.Информация;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		СтрокаСостояния = Результат.ТекстОшибки;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
КонецПроцедуры // Проверить()

&НаКлиенте
Процедура Регистрация(Команда)
	
	Результат = гЗарегистрироватьВОблаке(ТипОбработка, СеансовыеДанные, ИдентификаторСессии, email, пароль);
	
	Если Результат.Статус = "OK" Тогда 
		СтрокаСостояния = Результат.Информация;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Зеленый;
	Иначе
		СтрокаСостояния = Результат.ТекстОшибки;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
КонецПроцедуры // Регистрация()

&НаКлиенте
Процедура Войти(Команда)
	
	Результат = гПодключитьКОблаку(ТипОбработка, СеансовыеДанные, ИдентификаторСессии, email, пароль);
	
	Если Результат.Статус = "OK" Тогда 
		СтрокаСостояния = Результат.Информация;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Зеленый;
		
		ИдентификаторСессии = Результат.Соединение;
		
		СтруктураСохраняемыхЗначений = Новый Структура("email, ИдентификаторСессии", email, Результат.Соединение);
		СохранитьНастройкиПараметровПодключения(СтруктураСохраняемыхЗначений);
		Закрыть(СтруктураСохраняемыхЗначений);
	Иначе
		СтрокаСостояния = Результат.ТекстОшибки;
		Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Красный;
	КонецЕсли;
	
КонецПроцедуры // Войти()

&НаКлиенте
Процедура Выйти(Команда)
	
	Если Не ЗначениеЗаполнено(ИдентификаторСессии) Тогда 
		ПоказатьПредупреждение(, "Вы еще не входили.", 10, "Ошибка выхода из сервиса");
	Иначе
		Результат = гОтключитьсяОтОблака(ТипОбработка, СеансовыеДанные, ИдентификаторСессии);
		Если Результат.Статус = "OK" Тогда 
			СтрокаСостояния = Результат.Информация;
			Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Зеленый;
			
			ИдентификаторСессии = Неопределено;
			
			СтруктураСохраняемыхЗначений = Новый Структура("email, ИдентификаторСессии", Неопределено, Неопределено);			
			СохранитьНастройкиПараметровПодключения(СтруктураСохраняемыхЗначений);			
			Оповестить("ОбработатьИзменениеДанныеОПодключении", СтруктураСохраняемыхЗначений);						
		Иначе
			СтрокаСостояния = Результат.ТекстОшибки;
			Элементы.СтрокаСостояния.ЦветТекста = WebЦвета.Красный;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // Выйти()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция гВосстановитьНастройкиПараметровПодключения(ТипОбработка, СеансовыеДанные)
	
	Возврат ОбъектОбработки(ТипОбработка, СеансовыеДанные).гВосстановитьНастройкиПараметровПодключения();
	
КонецФункции // гВосстановитьНастройкиПараметровПодключения()

&НаСервереБезКонтекста
Функция гЗарегистрироватьВОблаке(ТипОбработка, СеансовыеДанные, соединение, email, пароль, УстановкаНовогоПароля = Ложь, НастройкиПрокси = Неопределено)
	
	Возврат ОбъектОбработки(ТипОбработка, СеансовыеДанные).гЗарегистрироватьВОблаке(соединение, email, пароль, УстановкаНовогоПароля, НастройкиПрокси);
	
КонецФункции

&НаСервереБезКонтекста
Функция гПодключитьКОблаку(ТипОбработка, СеансовыеДанные, соединение, email, пароль, тестирование = Ложь, НастройкиПрокси = Неопределено)
	
	Возврат ОбъектОбработки(ТипОбработка, СеансовыеДанные).гПодключитьКОблаку(соединение, email, пароль, тестирование, НастройкиПрокси);
	
КонецФункции

&НаСервереБезКонтекста
Функция гОтключитьсяОтОблака(ТипОбработка, СеансовыеДанные, ИдентификаторСессии)
	
	Возврат ОбъектОбработки(ТипОбработка, СеансовыеДанные).гОтключитьсяОтОблака(ИдентификаторСессии);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбъектОбработки(ТипОбработка, СеансовыеДанные)
	Результат = Новый (ЗначениеИзСтрокиВнутр(ТипОбработка));
	Результат.гСеансовыеДанные = СеансовыеДанные;
	Возврат Результат;
КонецФункции

// Процедура - Сохраняет настройки в хранилище общих настроек
&НаСервереБезКонтекста
Процедура СохранитьНастройкиПараметровПодключения(СтруктураСохраняемыхЗначений)
	
    ключОбъекта      = "Обработка.consquery83";
    ключНастроек     = "Обработка.consquery83.ФормаПодключенияПоследниеЗначения";
    описаниеНастроек = "ПоследниеЗначения";
    имяПользователя  = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;

    настройки = Новый Соответствие;
    настройки.Вставить("ФормаПодключенияПоследниеЗначения", СтруктураСохраняемыхЗначений);

    ХранилищеОбщихНастроек.Сохранить(ключОбъекта, ключНастроек, настройки, описаниеНастроек, имяПользователя);

КонецПроцедуры // СохранитьНастройкиПараметровПодключения()

#КонецОбласти